#!/usr/bin/env perl
use warnings;
use strict;
use open qw(locale);
use feature 'say';

#------------------------------------------------------------------------------
use File::Basename qw(basename);
our $PROGNAME = basename($0);

#------------------------------------------------------------------------------
our $nonAsciiPrintableOnly = 0;

#------------------------------------------------------------------------------
use Getopt::Long;
Getopt::Long::Configure('bundling', 'gnu_compat', 'no_ignore_case');
Getopt::Long::GetOptions(
    'N|non-ascii-printable-only' => sub { $nonAsciiPrintableOnly = 1; },
    'help'             => sub { usage(); exit(0); },
) or die("Type '$PROGNAME --help' for help.\n");

sub usage { print(<<"END"); }
usage:
    $PROGNAME [<option> ...] [<filename> ...]
options:
    -N, --non-ascii-printable-only
        --help
END

###############################################################################

use HTML::Entities qw(encode_entities);

while (<>) {
    s{\R\z}{};
    if ($nonAsciiPrintableOnly) {
        say encodeEntitiesNonAsciiPrintableOnly($_);
    } else {
        say encode_entities($_);
    }
}

sub encodeEntitiesNonAsciiPrintableOnly {
    my ($string) = @_;
    $string =~ s{[^\t\r\n -~]+}{encode_entities($&)}ge;
    return $string;
}
