#!/usr/bin/env perl
use warnings;
use strict;
use open IO => qw(:locale);
use HTML::Entities qw(encode_entities);

our $slurp = 0;
our $non_ascii_printable_only = 0;
our $ascii_only = 0;

use Getopt::Long;
Getopt::Long::Configure('gnu_getopt');
Getopt::Long::GetOptions(
    'z|slurp'                    => \$slurp,
    'N|non-ascii-printable-only' => sub { $non_ascii_printable_only = 1; $ascii_only = 0; },
    'A|ascii-only'               => sub { $non_ascii_printable_only = 0; $ascii_only = 1; },
    'help|?'                     => sub { usage(); exit(0); },
) or die("Type 'htmlescape --help' for help.\n");

sub usage { print(<<"END"); }
htmlescape [<option> ...] [<filename> ...]
-z, --slurp
-N, --non-ascii-printable-only
-A, --ascii-only
-?, --help
END

$/ = undef if $slurp;
while (<>) {
    s{\R\z}{} if !$slurp;
    print html_escape($_), "\n";
}

sub html_escape {
    my ($string) = @_;
    if ($non_ascii_printable_only) {
        $string =~ s{[^\t\r\n -~]+}{encode_entities($&)}ge;
        return $string;
    }
    if ($ascii_only) {
        $string =~ s{[&#'<>]+}{encode_entities($&)}ge;
        return $string;
    }
    return encode_entities($string);
}
