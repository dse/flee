#!/usr/bin/env perl
use warnings;
use strict;
use open IO => qw(locale);
use URI::Escape qw(uri_escape);

our $slurp = 0;
our $non_ascii_printable_only = 0;

use Getopt::Long;
Getopt::Long::Configure('gnu_getopt');
Getopt::Long::GetOptions(
    'z|slurp'                    => \$slurp,
    'N|non-ascii-printable-only' => \$non_ascii_printable_only,
    'help|?'                     => sub { usage(); exit(0); },
) or die("Type 'uriescape --help' for help.\n");

sub usage { print(<<"END"); }
uriescape [<option> ...] [<filename> ...]
-z, --slurp
-N, --non-ascii-printable-only
-?, --help
END

$/ = undef if $slurp;
while (<>) {
    s{\R\z}{} if !$slurp;
    if ($non_ascii_printable_only) {
        print uri_escape($_, "^ -~");
    } else {
        print uri_escape($_);
    }
    print "\n"  if !$slurp;
}
